// return 0 ~ 1
function vector selectedNoise(int noiseId; vector pos; float time; float flow) {
    int Random = 0,                 // random
        PerlinNoise =  1,           // noise
        OriginalPerlinNoise = 2,    // onoise
        SimplexNoise = 3,           // xnoise
        SparseConvolutionNoise = 4, // snoise
        FlowNoise = 5,              // flownoise
        CurlNoise = 6,              // curlnoise
        ArigatorNoise = 7;          // anoise

    if(Random == noiseId) {// 0 ~ 1
        return random(set(pos.x, pos.y, pos.z, time));
    }

    if(PerlinNoise == noiseId) { // 0 ~ 1
        return noise(set(pos.x, pos.y, pos.z, time));
    }

    if(OriginalPerlinNoise == noiseId) { // -1 ~ 1
        return onoise(pos);
    }

    if(SimplexNoise == noiseId) { // 0 ~ 1
        return xnoise(set(pos.x, pos.y, pos.z, time));
    }

    if(SparseConvolutionNoise == noiseId) { // -1.7 ~ 1.7
        return snoise(pos);
    }

    if(FlowNoise == noiseId) { // 0 ~ 1 (0.2 ~ 0.8), args(p, flow(0 ~ 1))
        return flownoise(set(pos.x, pos.y, pos.z, time), flow);
    }

    if(CurlNoise == noiseId) { // -1 ~ 1.02
        return curlnoise(set(pos.x, pos.y, pos.z, time));
    }

    if(ArigatorNoise == noiseId) { // anoise 0 ~ 1
        return anoise(
                pos
            );
    }

    error("Error: selected");
}

function vector fitTo01(int noiseId; vector vec) {
    int OriginalPerlinNoise = 2,    // onoise
        SparseConvolutionNoise = 4, // snoise
        CurlNoise = 6;              // curlnoise

    // -1 ~ 1
    if(OriginalPerlinNoise == noiseId) 
        return fit11(vec, {0,0,0}, {1,1,1});
    
    // -1.7 ~ 1.7
    if(SparseConvolutionNoise == noiseId) 
        return fit(vec, {-1.7, -1.7, -1.7}, {1.7, 1.7, 1.7}, {0,0,0}, {1,1,1});

    // -1 ~ 1.02
    if(CurlNoise == noiseId)
        return fit(vec, {-1, -1, -1}, {1.02, 1.02, 1.02}, {0,0,0}, {1,1,1});

    return vec; // 0 ~ 1
}

function vector sampling(int fbmId; vector v) {
    int None = 0,
        Normal = 1,
        Cauchy = 2,
        Sphere_Uniform = 3,
        Orientation_Uniform = 4;

    int samplingId = chi("fbm" + itoa(fbmId) + "_SamplingID");

    if (samplingId == Normal) {
        vector vec = fitTo01(chi("fbm" + itoa(fbmId) + "_NoiseID"), v);
        return sample_normal(vec);
    }
    
    if (samplingId == Cauchy) {
        return sample_cauchy(v);// -1 ~ 0 ~ 1
    }
    
    if (samplingId == Sphere_Uniform) {
        return sample_sphere_uniform(v);
    }
    
    if (samplingId == Orientation_Uniform) {
        vector vec = fitTo01(chi("fbm" + itoa(fbmId) + "_NoiseID"), v);
        return sample_orientation_uniform(vec);
    }

    error("ERROR: Sampling");
}

function vector fitTo11(int noiseId, samplingId; vector v) {
    int notSelectedSamplingId = 0;
    if(samplingId == notSelectedSamplingId) {
        int OriginalPerlinNoise = 2,    // onoise
            SparseConvolutionNoise = 4, // snoise
            CurlNoise = 6;              // curlnoise

        if((OriginalPerlinNoise != noiseId) && 
                (SparseConvolutionNoise != noiseId) &&
                (CurlNoise != noiseId)) {
            return fit01(v, {-1,-1,-1}, {1,1,1});
        }
    }
}

function vector fbm(int octave;
                    int noiseId;
                    vector p;
                    vector sft;
                    float t;
                    float flow) {
    vector pos = p;
    vector v = {0};
    vector a = {0.5, 0.5, 0.5};
    matrix mat = ident();
    // float radius = 3.1415;
    float time = t;
    float scale = 2;
    vector shift = sft;

    for (int i = 0; i < octave; ++i) {
        v += a * selectedNoise(noiseId, pos, time, flow);

        rotate(mat, 0.1, {0,1,0});

        pos = mat * pos * scale + shift;
        a *= 0.5;
        scale += 0.01;
    }

    return v / (1 - a);
}

function vector domainWarping(vector p; float time) {
    vector pos = p;

    vector fbm1 = fbm(
            chi("fbm1_Octave"),
            chi("fbm1_NoiseID"),
            pos * chf("fbm1_Scale"),
            chv("fbm1_Shift"),
            time,
            chf("fbm1_FlowNoise_Flow")
        );

    if (chi("fbm1_SamplingID") != 0) {
        fbm1 = sampling(1, fbm1);
    }
    // return fbm1; //debug

    vector fbm2 = fbm(
            chi("fbm2_Octave"),
            chi("fbm2_NoiseID"),
            pos + (fbm1 * chf("fbm2_Scale")),
            chv("fbm2_Shift"),
            time,
            chf("fbm2_FlowNoise_Flow")
        );

    if (chi("fbm2_SamplingID") != 0) {
        fbm2 = sampling(2, fbm2);
    }

    vector fbm3 = fbm(
            chi("fbm3_Octave"),
            chi("fbm3_NoiseID"),
            pos + (fbm2 * chf("fbm3_Scale")),
            chv("fbm3_Shift"),
            time,
            chf("fbm3_FlowNoise_Flow")
        );

    if (chi("fbm3_SamplingID") != 0) {
        fbm3 = sampling(3, fbm3);
    }

    if(chi("is_Fit_11")) {
       fbm3 = fitTo11(chi("fbm3_NoiseID"), chi("fbm3_SamplingID"), fbm3);
    }

    return fbm3;
}
